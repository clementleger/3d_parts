include <../common/roundedcube.scad>;
include <../common/prism.scad>;

MAX_7219_CASE_WIDTH = 128;
MAX_7219_CASE_HEIGHT = 32;
MAX_7219_CASE_THICKNESS = 13;

CASE_THICKNESS = 40;
CASE_BOTTOM_THICKNESS = 2;

BORDER_THICKNESS = 5;

FRONT_PANEL_BORDER_THICKNESS = 3;
FRONT_PANEL_THICKNESS = 2.6;

BOX_FEET_HEIGHT = 4;
BOX_FEET_WIDTH = 38;
BOX_FEET_THICKNESS = 10;
BOX_FEET_MIDDLE_SPACING = 70;

DEPTH_END_WIDTH = MAX_7219_CASE_WIDTH;
DEPTH_END_HEIGHT = 1;
DEPTH_END_THICKNESS = 3;


CABLE_HOLE_DIAMETER = 15;

$fn = 80;

module box() {
    difference() {
        /* External box */
        roundedcube([MAX_7219_CASE_WIDTH + 2 * BORDER_THICKNESS, MAX_7219_CASE_HEIGHT + 2 * BORDER_THICKNESS, CASE_THICKNESS], false, 6, "z");

        /* screen box */
        translate([BORDER_THICKNESS - FRONT_PANEL_BORDER_THICKNESS, BORDER_THICKNESS - FRONT_PANEL_BORDER_THICKNESS, CASE_THICKNESS - FRONT_PANEL_THICKNESS]) roundedcube([MAX_7219_CASE_WIDTH + 2 * FRONT_PANEL_BORDER_THICKNESS, MAX_7219_CASE_HEIGHT + 2 * FRONT_PANEL_BORDER_THICKNESS, CASE_THICKNESS], false, 6, "z");

        
        
        /* External box */
        translate([BORDER_THICKNESS, BORDER_THICKNESS, CASE_BOTTOM_THICKNESS]) cube([MAX_7219_CASE_WIDTH, MAX_7219_CASE_HEIGHT, CASE_THICKNESS], false);

        translate([(MAX_7219_CASE_WIDTH + 2 * BORDER_THICKNESS)/2, (MAX_7219_CASE_HEIGHT + 2 * BORDER_THICKNESS)/2, 0]) cylinder(d = CABLE_HOLE_DIAMETER, h = CASE_BOTTOM_THICKNESS);
    }
       
    /* Feet */
    translate([(MAX_7219_CASE_WIDTH + 2 * BORDER_THICKNESS)/2 - BOX_FEET_THICKNESS/2 - BOX_FEET_MIDDLE_SPACING/2, -BOX_FEET_HEIGHT, BOX_FEET_WIDTH]) mirror([0, 0, 1]) prism(BOX_FEET_THICKNESS, BOX_FEET_HEIGHT, BOX_FEET_WIDTH);
    translate([(MAX_7219_CASE_WIDTH + 2 * BORDER_THICKNESS)/2 - BOX_FEET_THICKNESS/2 + BOX_FEET_MIDDLE_SPACING/2, -BOX_FEET_HEIGHT, BOX_FEET_WIDTH]) mirror([0, 0, 1]) prism(BOX_FEET_THICKNESS, BOX_FEET_HEIGHT, BOX_FEET_WIDTH);

    /* Depth end for screen */
    translate([BORDER_THICKNESS + MAX_7219_CASE_WIDTH/2 - DEPTH_END_WIDTH /2, BORDER_THICKNESS, CASE_THICKNESS - DEPTH_END_THICKNESS - FRONT_PANEL_THICKNESS - MAX_7219_CASE_THICKNESS]) cube([DEPTH_END_WIDTH, DEPTH_END_HEIGHT, DEPTH_END_THICKNESS]);
    translate([BORDER_THICKNESS + MAX_7219_CASE_WIDTH/2 - DEPTH_END_WIDTH /2, BORDER_THICKNESS + MAX_7219_CASE_HEIGHT - DEPTH_END_HEIGHT, CASE_THICKNESS - DEPTH_END_THICKNESS - FRONT_PANEL_THICKNESS - MAX_7219_CASE_THICKNESS]) cube([DEPTH_END_WIDTH, DEPTH_END_HEIGHT, DEPTH_END_THICKNESS]); 
}


box();